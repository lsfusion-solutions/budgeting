MODULE BudgetRent;

REQUIRE BudgetLocation, BudgetEmployee, CostItemEmployee, Utils;

NAMESPACE Budget;

rentCurrency 'Rent currency' = DATA Currency (Location); 
nameRentCurrency 'Rent currency' (Location l) = name(rentCurrency(l));

costItem 'Cost type' = DATA CostItem (Location);
nameCostItem 'Cost type' (Location l) = canonicalName(costItem(l));

rent 'Rent' = DATA NUMERIC[16,3] (Budget, Location);
rentUSD 'Rent, USD' (Budget b, Location l) = round0(rent(b, l) / defaultRateOn(rentCurrency(l), fromDate(b)));

countEmployees 'Count of employees (with salary)' (Budget b, Location l) = GROUP SUM 1 IF hasSalary(Employee e, b) BY location(e, b);  
employeesCount 'Employees count (total)' (Location l, Budget b) = GROUP SUM 1 BY location(Employee e, b); 
rentPerEmployeeUSD 'Rent per employee, USD' (Budget b, Location l) = rentUSD(b, l) / employeesCount(l, b);

EXTEND FORM location
    PROPERTIES(c) nameRentCurrency, nameCostItem
    PROPERTIES(b, c) rent, countEmployees
    PROPERTIES(c, b) employeesCount  
    PROPERTIES(b, c) rentPerEmployeeUSD
;

rentCostItem 'Rent cost type' = DATA CostItem (CostItem);
nameRentCostItem 'Rent cost type' (CostItem i) = canonicalName(rentCostItem(i));
EXTEND FORM costItem
    PROPERTIES(c) nameRentCostItem
;

rentCostItem 'Rent cost type' (Employee e, Budget b) = rentCostItem(costItem(e, b));
nameRentCostItem 'Rent cost type' (Employee e, Budget b) = canonicalName(rentCostItem(e, b));
