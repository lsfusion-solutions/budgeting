MODULE BudgetAccount;

REQUIRE Account, Budget, BudgetBI;

NAMESPACE Budget;

balance 'Balance' = DATA NUMERIC[16,2](Budget, Account);
receipt 'Receipt' = DATA NUMERIC[16,2](Budget, Account);

balanceUSD 'Balance (USD)' (Budget b, Account a) =  round2(balance(b, a) / defaultRateOn(currency(a), fromDate(b)));
receiptUSD 'Receipt' (Budget b, Account a) =  round2(receipt(b, a) / defaultRateOn(currency(a), fromDate(b)));

FORM cashFlow 'Cash Flow'
    
//    OBJECTS dates = (df = DATE, dt = DATE) PANEL 
//    PROPERTIES VALUE (df), VALUE (dt)
    
    OBJECTS a = Account    
    OBJECTS b = Budget
    PROPERTIES (b) READONLY fromDate, toDate, name
    PROPERTIES (b, a) balance COLUMNS (a) HEADER 'Balance ' + name(a) + ' (' + nameCurrency(a) + ')',
                      balanceUSD COLUMNS (a) HEADER 'Balance ' + name(a) + ' (USD)' BACKGROUND RGB (192, 255, 192),// SHOWIF NOT nameCurrency(a) == 'USD',
                      receipt COLUMNS (a) HEADER 'Receipt ' + name(a) + ' (' + nameCurrency(a) + ')',
                      receiptUSD COLUMNS (a) HEADER 'Receipt ' + name(a) + ' (USD)' BACKGROUND RGB (192, 255, 192)// SHOWIF NOT nameCurrency(a) == 'USD'
;

balanceUSD 'Balance, USD' (Budget b) = GROUP SUM balanceUSD(b, Account a);
receiptUSD 'Receipt, USD' (Budget b) = GROUP SUM receiptUSD(b, Account a);
receiptUSD 'Receipt from, USD' (Budget bfrom, Budget bto) = GROUP SUM receiptUSD(Budget bb, Account a) IF toDate(bb) >= toDate(bfrom) AND toDate(bb) < toDate(bto);
planned 'Planned from, USD' (Budget bfrom, Budget bto) = GROUP SUM planned(Budget bb) IF toDate(bb) >= toDate(bfrom) AND toDate(bb) < toDate(bto);

estimated 'Estimated value, USD' (Budget bfrom, Budget b) = (balanceUSD(bfrom) IF fromDate(b) >= fromDate(bfrom)) (+) receiptUSD(bfrom, b) (-) planned(bfrom, b);

capMonth (Period p) = round2((GROUP SUM overTotalChildCap(p, CostItem i) IF NOT parent(i)) / budgetCount(p));
capAverage 'Average cap, USD' (CostItem c, Budget b) = round2(GROUP SUM (overTotalChildCap(Period p, c) / budgetCount(p)) IF NOT selected(p, b));
capAverage 'Average cap, USD' (Budget b) = GROUP SUM capMonth(Period p) IF selected(p, b);
plannedAverage 'Average planned from, USD' (Budget bfrom, Budget bto) = GROUP SUM capAverage(Budget bb) IF toDate(bb) >= toDate(bfrom) AND toDate(bb) < toDate(bto);
estimatedAverage 'Average estimated value, USD' (Budget bfrom, Budget b) = (balanceUSD(bfrom) IF fromDate(b) >= fromDate(bfrom)) (+) receiptUSD(bfrom, b) (-) plannedAverage(bfrom, b);

EXTEND FORM cashFlow
    OBJECTS bp = Budget PANEL 
    PROPERTIES (bp) name SELECTOR 
    
    OBJECTS aa = Account 
    OBJECTS bb = Budget
    PROPERTIES (bb) READONLY fromDate, toDate, name, balanceUSD, receiptUSD, spent
    PROPERTIES receiptUSD(bp, bb)
    PROPERTIES BACKGROUND RGB (255,255,222) planned(bb), planned(bp, bb), estimated(bp, bb)
    PROPERTIES READONLY capAverage(bb), plannedAverage(bp, bb), estimatedAverage(bp, bb)
;

DESIGN cashFlow {
    OBJECTS {
    type = TABBED;
    NEW flow {
        caption = 'Cash flow';
        MOVE BOX (b);
    }
    NEW flows {
        caption = 'Flows';
        MOVE BOX (bp);
        MOVE BOX (bb);
    }
    }
}

NAVIGATOR {
    NEW cashFlow;
}