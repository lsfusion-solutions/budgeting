MODULE BudgetContractScoring;

REQUIRE BudgetExpenseReconcileContractor;

NAMESPACE Budget;

CLASS Scoring 'Contractors Scoring';
TABLE scoring (Scoring);

number '#' = DATA INTEGER (Scoring) CHARWIDTH 2;
criteria 'Criteria' = DATA ISTRING (Scoring) CHARWIDTH 50;
criteriaText 'Criteria' (Scoring s) = TEXT (criteria(s));
short 'Criteria name' = DATA ISTRING[15] (Scoring) CHARWIDTH 15;

scoring 'Scoring' = DATA BOOLEAN (Contractor);

criteriaAmountNum 'Amount of Criteria' = DATA INTEGER ();

TABLE assessmentOverall (Contractor, Employee);
TABLE assessmentScore (Contractor, Employee, Scoring);
TABLE assessmentScoreLog (Contractor, Employee, Scoring, INTEGER);

comment 'Comment' = DATA TEXT (Contractor, Employee);
dateTimeAssessment 'Date/Time Assessment' = DATA DATETIME (Contractor, Employee);
assessmentScoreNum 'Assessment' = DATA INTEGER (Contractor, Employee, Scoring);

votedCriteria 'Number of criteria voted' = GROUP SUM 1 IF assessmentScoreNum (Contractor c, Employee e, Scoring s) BY c,e;
votedAllCriteria (Contractor c, Employee e) = votedCriteria(c,e) == criteriaAmountNum();

voted 'Voted' = GROUP SUM 1 IF votedAllCriteria(Contractor c, Employee e) BY c,e;
voted 'Voted' = GROUP SUM 1 IF votedAllCriteria(Contractor c, Employee e) BY c; 
assessmentOverall 'Overall' = GROUP SUM assessmentScoreNum(Contractor c, Employee e, Scoring s) IF votedAllCriteria(c,e) BY c,e;
assessment 'Assessment' (Contractor c, Employee e) = NUMERIC[17,2](NUMERIC[17,2](assessmentOverall(c,e)) / voted(c,e) / criteriaAmountNum());
assessmentOverall 'Assessment' = GROUP SUM assessment(Contractor c, Employee e) IF votedAllCriteria(c,e) BY c;
assessment 'Assessment' (Contractor c) = assessmentOverall(c) / voted(c);

commentOverall 'All comments' = GROUP CONCAT name(Employee e) + ', ' + dateTimeAssessment(Contractor c, e) + ':\n' + comment(c, e),'\n\n' IF votedAllCriteria(c,e) AND comment(c, e) ORDER e BY c;

assignSummary 'Number of Expenses linked' = GROUP SUM assigned(Expense s, Employee e) IF contractor(s) == Contractor c BY c,e; // AND scoring(c)
assignScoring 'Ask Scoring' = DATA BOOLEAN (Contractor,Employee);
showScoring 'Participate Scoring' (Contractor c,Employee em) = assignSummary(c,em) > 0 OR assignScoring(c,em);

// Managers of the Contractor
CLASS Manager 'Managers';
TABLE manager (Manager);

contractor 'Contractor' = DATA Contractor(Manager);
name 'Name' = DATA ISTRING[30] (Manager);
email 'Email' = DATA ISTRING[30] (Manager);
phone 'Phone' = DATA ISTRING[30] (Manager);
comment 'Comment' = DATA ISTRING[30] (Manager);

assessment 'Search' = DATA BOOLEAN (Contractor, Employee, Scoring, INTEGER);
range 'Range for assessment' (INTEGER i) = GROUP MAX i IF iterate(i, 1, 5);
ballVal 'Value' (INTEGER i) = STRING[1](i);

WHEN LOCAL SET (assessment(Contractor c, Employee em, Scoring s, INTEGER i)) OR DROPCHANGED (assessment(c, em, s, i)) DO {
    FOR iterate(INTEGER var, 1, 5) DO {
        IF i == var THEN {
            IF assessment(c,em,s,i) THEN assessmentScoreNum(c,em,s) <- i;
            ELSE assessmentScoreNum(c,em,s) <- NULL;
        } 
        ELSE assessment(c,em,s,var) <- NULL;
    }
}

WHEN SETCHANGED (comment(Contractor c, Employee e)) DO {
    dateTimeAssessment(c,e) <- currentDateTime();
}

CONSTRAINT votedAllCriteria(Contractor c,Employee e) AND NOT comment(c,e) MESSAGE 'Please provide Your Comment';

EXTEND FORM contractors
    FILTERGROUP scoring
        FILTER 'Show Contractors to Score' showScoring(c,currentUser())
        FILTER 'Show remaining Contractors to Score' showScoring(c,currentUser()) AND NOT votedAllCriteria(c,currentUser())

    OBJECTS em = Employee
    FILTERS em == currentUser()

    PROPERTIES(c) assessment AFTER name(c), commentOverall PANEL 
    PROPERTIES(c) READONLYIF NOT enableEditContract() scoring AFTER assessment(c) SHOWIF afg(currentUser())
    
    OBJECTS ball = INTEGER
    PROPERTIES range(ball) SHOWIF NULL
    ORDERS range(ball)
    FILTERS range(ball)
    
    OBJECTS s = Scoring
    PROPERTIES (s) SHOWIF showScoring(c,em) READONLY number, criteria // criteriaText //
    PROPERTIES SHOWIF showScoring(c,em) assessment(c,em,s,ball) COLUMNS (ball) HEADER range(ball)
    PROPERTIES SHOWIF showScoring(c,em) comment(c,em) PANEL // assessmentScoreNum(c,em,s)
    ORDERS number(s)
    
    OBJECTS m = Manager
    PROPERTIES (m)  name, email, phone, comment, NEW, DELETE // READONLYIF NOT showScoring(c,em) // SHOWIF showScoring(c,em)
    FILTERS contractor(m) == c
    
    OBJECTS eml = Employee
    PROPERTIES READONLY name(eml)
    PROPERTIES(c,eml) SHOWIF afg(currentUser()) assignSummary, assignScoring, showScoring
    PROPERTIES(c,eml) assessment//, comment PANEL
    PROPERTIES READONLY assessmentScoreNum(c,eml,s) COLUMNS (s) HEADER short(s)
    ORDERS name(eml)
    FILTERS afg(currentUser()) OR assessment(c,eml)
;

DESIGN contractors {
    detailContractor {    
        NEW scoring FIRST { //  AFTER BOX (e)
            caption = 'Scoring';
            fill = 1;
            MOVE BOX (s) { fill = 1; }
            NEW scoreTab {
                caption = 'Experience';
                type = TABBED;
                fill = 1;
                alignment = STRETCH;
                NEW commentEmployee {
                    caption = 'Your Comment';
                    fill = 1;
                    alignment = STRETCH;
                    MOVE PROPERTY (comment(c,em)) { caption = ''; alignment = STRETCH; fill = 1; }
                }
                NEW commentOverall {
                    caption = 'Overall Comment';
                    fill = 1;
                    alignment = STRETCH;
                    MOVE PROPERTY (commentOverall(c)) { caption = ''; alignment = STRETCH; fill = 1;}
                }
                MOVE BOX (m) {
                    fill = 1;
                    alignment = STRETCH;
                }
                MOVE BOX (eml) {
                    fill = 1;
                    alignment = STRETCH;
                }
            }
        }
    }
}

FORM criteria 'Scoring Criteria'
    PROPERTIES criteriaAmountNum()
    
    OBJECTS s = Scoring
    PROPERTIES (s) number, criteria, short, NEW , DELETE 
;

NAVIGATOR { 
    masterData {
        cycles {
            NEW criteria;
        }
    }
}