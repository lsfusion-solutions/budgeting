MODULE BudgetPurchaseInvoice;

REQUIRE Budget, PurchaseInvoice, Utils;

NAMESPACE Budget;

costCenter 'Cost center' = DATA CostCenter (Invoice);
nameCostCenter 'Cost center' (Invoice i) = name(costCenter(i));

budgetDetail 'Budget detail' = DATA BudgetDetail (Invoice);

CONSTRAINT costCenter(Invoice i) != costCenter(budgetDetail(i)) CHECKED BY budgetDetail[Invoice],costCenter[Invoice] MESSAGE 'Cost center is not equal to cost center of budget detail'; 
WHEN LOCAL CHANGED (budgetDetail(Invoice i)) DO { costCenter(i) <- costCenter(budgetDetail(i)); }

completeNameBudgetDetail 'Budget detail' (Invoice i) = completeName(budgetDetail(i));
nameBudget 'Budget' (Invoice i) = nameBudget(budgetDetail(i));
itemBudgetDetail 'Budget item' (Invoice i) = name(budgetDetail(i));

EXTEND FORM invoice
    PROPERTIES(i) nameCostCenter, completeNameBudgetDetail
;
DESIGN invoice {
    OBJECTS {
        invoice {
            NEW budget AFTER file {
                alignment = STRETCH;
                caption = 'Budget';
                MOVE PROPERTY(nameCostCenter(i));
                MOVE PROPERTY(completeNameBudgetDetail(i));
            }
        }
    }
}

EXTEND FORM invoices
    PROPERTIES(i) READONLY nameCostCenter, completeNameBudgetDetail
;

invoiced 'Invoiced, USD' (BudgetDetail d) = GROUP SUM round0(amount(Invoice i) / defaultRateOn(currency(i), date(i))) BY budgetDetail(i); 
invoiced 'Invoiced, USD' (Budget b, CostCenter cp) = GROUP SUM invoiced(BudgetDetail d) BY budget(d), costCenter(d);
//EXTEND FORM budgetDetails PROPERTIES(d) READONLY invoiced;

overCopy(Invoice ni, Invoice i) + {
    costCenter(ni) <- costCenter(i);
    budgetDetail(ni) <- budgetDetail(i);
}