MODULE CurrencyB;

REQUIRE System, MasterData, Time, Utils;

NAMESPACE Currency;

CLASS Currency 'Currency';
TABLE currency(Currency);

name 'Name' = DATA ISTRING[50](Currency) IN id CHARWIDTH 5;
currencyName = GROUP AGGR Currency currency BY name(currency);

shortName 'Short name' = DATA BPSTRING[3] (Currency);
currencyShortName (string) = GROUP AGGR Currency currency WHERE currency IS Currency BY shortName(currency);

// Формы

FORM currency 'Currency'
    OBJECTS c = Currency PANEL
    PROPERTIES(c) name, shortName

    EDIT Currency OBJECT c
;

FORM currencies 'Currencies'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY name, shortName

    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
;

FORM dialogCurrency 'Select currency'
    OBJECTS c = Currency
    PROPERTIES(c) READONLY name
    
    LIST Currency OBJECT c
;

// ---------------------------------- Типы обмена ---------------------------- //

CLASS TypeExchange 'Exchange rate';
TABLE typeExchange(TypeExchange);
TABLE rateExchange (TypeExchange, Currency, DATE);

name 'Name' = DATA ISTRING[50](TypeExchange) IN id;

currency 'Currency' = DATA Currency (TypeExchange);
nameCurrency 'Currency' (TypeExchange typeExchange) = name(currency(typeExchange)) IN base;

FORM typeExchange 'Тип обмена'
    OBJECTS t = TypeExchange PANEL
    PROPERTIES(t) name, nameCurrency
    
    EDIT TypeExchange OBJECT t
;

// Date

rate 'Rate' = DATA NUMERIC[20,13] (TypeExchange, Currency, DATE);
typeExchange (name) = GROUP AGGR TypeExchange typeExchange WHERE typeExchange IS TypeExchange BY name(typeExchange);

rateOn 'Rate' (TypeExchange typeExchange, Currency currency, DATE date) =
    GROUP LAST rate(typeExchange, currency, DATE dateIn)
          ORDER dateIn
          WHERE rate(typeExchange, currency, dateIn) AND dateIn <= date COMPLEX;

curRate 'Rate' (TypeExchange typeExchange, Currency currency) =
    rateOn(typeExchange, currency, currentDate());

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Exchange rate'
    OBJECTS t = TypeExchange PANEL
    OBJECTS c = Currency PANEL
    OBJECTS d = DATE PANEL
    PROPERTIES READONLY name(t),  nameCurrency(t), name(c)
    PROPERTIES val=VALUE(d), rate(t,c,d)

;
DESIGN dialogTypeExchangeCurrency {
    BOX {
        MOVE BOX(t) {
            MOVE PROPERTY(name(t)) { focusable = FALSE; charWidth = 40;}
        }
        MOVE BOX(c){
            horizontal = FALSE;
            MOVE BOX(d) {
                horizontal = TRUE;

                MOVE PROPERTY(rate(t,c,d));
            }
            NEW row {
                caption = 'Currencies';
                horizontal = TRUE;
                MOVE PROPERTY(name(c)) { caption = 'From'; charWidth = 20;}
                MOVE PROPERTY(nameCurrency(t)) { caption = 'To'; charWidth = 20;}
            }
        }
    }
    MOVE TOOLBARBOX;
}

dialog 'Add' (TypeExchange typeExchange, Currency currency)  { SHOW dialogTypeExchangeCurrency OBJECTS t = typeExchange, c = currency; }  TOOLBAR;
delete 'Delete' (TypeExchange typeExchange, Currency currency, DATE date)  { rate(typeExchange, currency, date) <- NULL; } IMAGE 'delete.png';


FORM typeExchangeCurrencyDate 'Exchange rate'

    OBJECTS t = TypeExchange
    PROPERTIES(t) READONLY name, nameCurrency
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE 

    OBJECTS c = Currency
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    PROPERTIES(t, c) READONLY curRate

    OBJECTS d = DATE
    PROPERTIES(d) READONLY VALUE

    PROPERTIES(t, c, d) rate
    PROPERTIES(t, c) dialog DRAW d 
    PROPERTIES(t, c, d) delete GRID

    FILTERS rate(t, c, d)

    LIST TypeExchange OBJECT t

;

DESIGN typeExchangeCurrencyDate {
    BOX {
        horizontal = FALSE;
        NEW split {
            fill = 1;
            MOVE BOX(t);
            NEW topContainer {
                fill = 2;
                tabbed = TRUE;
                NEW currentExchangeRates {
                    caption = 'Current rates';
                    horizontal = TRUE;
                    MOVE BOX(c);
                    MOVE BOX(d);
                }
            }
        }
        MOVE TOOLBARBOX;
    }
}

NAVIGATOR {
    masterData {
        NEW FOLDER currencyNavigator 'Currencies' {
            NEW currencies;
            NEW typeExchangeCurrencyDate;
        }
    }
}

// Значения по умолчанию

defaultCurrency = DATA Currency (); 
nameDefaultCurrency 'Default currency' = name(defaultCurrency()) CHARWIDTH 15;

defaultTypeExchange = DATA TypeExchange (); 
nameDefaultTypeExchange 'Default exchange rate' = name(defaultTypeExchange()) CHARWIDTH 15;

defaultRateOn (Currency c, DATE d) = rateOn(defaultTypeExchange(), c, d);

EXTEND FORM options
    PROPERTIES() nameDefaultCurrency, nameDefaultTypeExchange
;

DESIGN options {
    commons {
        MOVE PROPERTY(nameDefaultCurrency());
        MOVE PROPERTY(nameDefaultTypeExchange());
    }
}

extraSID 'CBRF code' = DATA BPSTRING[6] (Currency);
currencyExtraSID (string) = GROUP AGGR Currency currency WHERE currency IS Currency BY extraSID(currency);

EXTEND FORM currency PROPERTIES (c) extraSID;

EXTEND FORM currencies PROPERTIES(c) READONLY extraSID;

//----------------------------------------------- Импорт курсов ----------------------------------------------------
importCBRFExchangeRateDateFrom 'Date from' = DATA DATE ();
importCBRFExchangeRateDateTo 'Date to' = DATA DATE ();

numCode = DATA LOCAL STRING(INTEGER);
charCode = DATA LOCAL STRING(INTEGER);
nominal = DATA LOCAL STRING(INTEGER);
name = DATA LOCAL STRING(INTEGER);
value = DATA LOCAL STRING(INTEGER);
ID = DATA LOCAL STRING(INTEGER);
date = DATA LOCAL STRING();
name1 = DATA LOCAL STRING();

FORM valCurs FORMEXTID 'ValCurs'
PROPERTIES() date EXTID 'Date' ATTR, name1 EXTID 'name' ATTR

OBJECTS valute = INTEGER EXTID 'Valute'
PROPERTIES(valute) numCode EXTID 'NumCode', charCode EXTID 'CharCode', nominal EXTID 'Nominal', name EXTID 'Name', value EXTID 'Value', ID ATTR
;

importCBRFExchangeRate 'Import CBRF exchange rates (current)'()  { 
    NEWSESSION {
        LOCAL xml = FILE ();
                
        EXTERNAL HTTP GET 'http://www.cbr.ru/scripts/XML_daily.asp?' TO xml; 
        IMPORT valCurs XML FROM xml();
        
        LOCAL type = TypeExchange();
        type() <- GROUP MAX TypeExchange t IF currency(t) == currencyName('RUB');
        
        IF NOT type() THEN {
            MESSAGE 'Need to create exchange rate with currency RUB';
            BREAK;
        } 
        ELSE {
            FOR [GROUP SUM 1 IF (name(Currency c) == name(INTEGER i) OR numCode(i) == extraSID(c) OR name(c) == charCode(i)) 
                BY i, c](INTEGER i, Currency c) DO {
                value(i) <- replace(value(i), ',', '.');
                rate(type(), c, toDateFormat(date(), 'dd-MM-yyyy')) <- NUMERIC[20,13](NUMERIC[8,6](value(i)) / INTEGER(nominal(i)));
                name(c) <- STRING[50](name(i));
                extraSID(c) <- BPSTRING[6](numCode(i));
                name(c) <- BPSTRING[3](charCode(i));                
            }
            rate(type(), currencyName('RUB'), toDateFormat(date(), 'dd-MM-yyyy')) <- 1;
        }
        APPLY;
    } 
}

importCBRFExchangeRateDates 'Import CBRF exchange rates (dates)'()  { 
    LOCAL type = TypeExchange();
    type() <- GROUP MAX TypeExchange t IF currency(t) == currencyName('RUB');
    
    IF NOT type() THEN {
        MESSAGE 'Need to create exchange rate with currency RUB';
        BREAK;
    } ELSE FOR iterate(DATE d, importCBRFExchangeRateDateFrom(), importCBRFExchangeRateDateTo()) DO {
        LOCAL xml = FILE ();
                        
        EXTERNAL HTTP GET 'http://www.cbr.ru/scripts/XML_daily.asp?date_req=' + toChar(d, 'dd/MM/yyyy') TO xml; 
        IMPORT valCurs XML FROM xml();
         
        FOR [GROUP SUM 1 IF (name(Currency c) == name(INTEGER i) OR numCode(i) == extraSID(c) OR name(c) == charCode(i)) 
        BY i, c](INTEGER i, Currency c) DO {
            value(i) <- replace(value(i), ',', '.');
            rate(type(), c, toDateFormat(date(), 'dd-MM-yyyy')) <- NUMERIC[20,13](NUMERIC[8,6](value(i)) / INTEGER(nominal(i)));
            name(c) <- STRING[50](name(i));
            extraSID(c) <- BPSTRING[6](numCode(i));
            name(c) <- BPSTRING[3](charCode(i));                
        }
        rate(type(), currencyName('RUB'), toDateFormat(date(), 'dd-MM-yyyy')) <- 1;
        
    }
    EXEC apply();
}

EXTEND FORM typeExchangeCurrencyDate
    PROPERTIES() importCBRFExchangeRate, importCBRFExchangeRateDateFrom, importCBRFExchangeRateDateTo, importCBRFExchangeRateDates
;

DESIGN typeExchangeCurrencyDate {
    topContainer {
        NEW import {
            caption = 'Import';
            MOVE PROPERTY(importCBRFExchangeRate());
            NEW dates {
                horizontal = TRUE;
                MOVE PROPERTY(importCBRFExchangeRateDateFrom()) { align = CENTER; }
                MOVE PROPERTY(importCBRFExchangeRateDateTo()) { align = CENTER; }
                MOVE PROPERTY(importCBRFExchangeRateDates());
            }
        }
    }
}