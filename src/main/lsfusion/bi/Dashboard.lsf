MODULE Dashboard;

REQUIRE CashFlowBI, Expense;

NAMESPACE BudgetBI;

// Months

budgetByDate = budget(date(ExpenseLedger e));

// Metrics

monthExpenses 'Expences by Cost Center in Currecy by Month, USD' = GROUP SUM amountUSD(ExpenseLedger e) IF NOT skip(e) BY budgetByDate(e), currency(e), costCenter(e);

monthExpenses 'Expences by Cost Type by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency cur, CostCenter c) BY b,c;

monthExpenses 'Expences by Cost Type in Currecy by Month, USD' = GROUP SUM amountUSD(ExpenseLedger e) IF NOT skip(e) BY budgetByDate(e), currency(e), costItem(e);
            
monthExpenses 'Expences in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c, CostItem t) BY b, c;

monthExpenses 'Expences in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c) BY b;

monthCostItem 'Expences by top Cost Type in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c, CostItem t) BY b, c, costItem3(t);
monthCostItem 'Expences by top Cost Type in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c, CostItem t) BY b, costItem3(t);

monthCostRootItem 'Expences by top Cost Type in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c, CostItem t) BY b, c, costItem2(t);
monthCostRootItem 'Expences by top Cost Type in Currecy by Month, USD' = GROUP SUM monthExpenses(Budget b, Currency c, CostItem t) BY b, costItem2(t);

// --- Details Pop-up ----

FORM dashUpCostItem 'Expense Details by Cost Type'
    OBJECTS b = Budget PANEL
    OBJECTS t = CostItem PANEL 
    
    OBJECTS e = ExpenseLedger
    PROPERTIES(e) READONLY date, amountUSD, note, nameCostItem, nameCostCenter, info, reference, nameCurrency, amount
    FILTERS NOT skip(e),
            budgetByDate(e) == b,
            level(costItem(e), t),
            (hideSensitive(mainRole(currentUser())) AND NOT sensitive(e)) OR NOT hideSensitive(mainRole(currentUser())) 
    ORDERS amountUSD(e) DESC
;

dashUpCostItem 'Expense Details by Cost Type' (Budget b, CostItem t) {
    DIALOG dashUpCostItem OBJECTS b = b, t = t FLOAT MANAGESESSION;
};

FORM dashUpCostCenter 'Expense Details by Cost Center'
    OBJECTS b = Budget PANEL
    OBJECTS c = CostCenter PANEL 
    
    OBJECTS e = ExpenseLedger
    PROPERTIES(e) READONLY date, amountUSD, note, nameCostItem, info, reference, nameCurrency, amount
    FILTERS NOT skip(e),
            budgetByDate(e) == b,
            costCenter(e) == c,
            (hideSensitive(mainRole(currentUser())) AND NOT sensitive(e)) OR NOT hideSensitive(mainRole(currentUser())) 
    ORDERS amountUSD(e) DESC
;

dashUpCostCenter 'Expense Details by Cost Center' (Budget b, CostCenter c) {
    DIALOG dashUpCostCenter OBJECTS b = b, c = c FLOAT MANAGESESSION; //t = NULL, , b = NULL
};

FORM dashUpBudget 'Expense Details'
    OBJECTS b = Budget PANEL 
    
    OBJECTS e = ExpenseLedger
    PROPERTIES(e) READONLY date, amountUSD, nameCostItem, nameCostCenter, note, reference, info, nameCurrency, amount
    FILTERS NOT skip(e),
            budgetByDate(e) == b,
            (hideSensitive(mainRole(currentUser())) AND NOT sensitive(e)) OR NOT hideSensitive(mainRole(currentUser())) 
    ORDERS amountUSD(e) DESC
;

dashUpBudget 'Expense Details by Month' (Budget b) {
    DIALOG dashUpBudget OBJECTS b = b FLOAT MANAGESESSION;
};

// ---- Dashboard ----

filterBudgetMonths 'Do not filter Budget Months' = DATA LOCAL NESTED BOOLEAN ();
filterBudgetMonths 'Show' = DATA LOCAL NESTED BOOLEAN (Budget);

FORM dashboard 'Dashboard'
    OBJECTS b = Budget PANEL 
    PROPERTIES name(b) SELECTOR, planned(b), sumUSD=monthExpenses(b,currencyName('USD')), sumRUR=monthExpenses(b,currencyName('RUR')) , monthExpenses(b)
    
    OBJECTS t = CostItem 
    PROPERTIES ON CHANGE dashUpCostItem(b,t) name(t), planned(b,t), monthCostRootItem(b,t), sumRootUSD=monthCostRootItem(b,currencyName('USD'),t), sumRootRUB=monthCostRootItem(b,currencyName('RUR'),t), orderLevel(t) SHOWIF NULL
    ORDERS orderLevel(t)
    FILTERS costItem2(t) == t AND monthCostRootItem(b,t) > 0
    
    OBJECTS i = CostItem 
    PROPERTIES ON CHANGE dashUpCostItem(b,i) nameCostItem2(i), name(i), planned(b,i), sumItemUSD=monthCostItem(b,currencyName('USD'),i), sumItemRUB=monthCostItem(b,currencyName('RUR'),i), orderLevel(i) SHOWIF NULL
    ORDERS orderLevel(i)
    FILTERS costItem3(i) == i AND monthCostItem(b,i) > 0
    
    OBJECTS c = CostCenter 
    PROPERTIES ON CHANGE dashUpCostCenter(b,c) name(c), planned(b,c), sumCenterUSD=monthExpenses(b,currencyName('USD'),c), sumCenterRUB=monthExpenses(b,currencyName('RUR'),c)
    ORDERS name(c)
    FILTERS monthExpenses(b,c) > 0
    
    OBJECTS bm = Budget
    PROPERTIES (bm) filterBudgetMonths SHOWIF filterBudgetMonths()
    PROPERTIES (bm) ON CHANGE dashUpBudget(bm) name, planned, indexMonth SHOWIF NULL, monthExpenses
    ORDERS indexMonth(bm) DESC
    //FILTERS indexMonth(bm) >= (indexMonth(b) - 2) AND indexMonth(bm) <= (indexMonth(b) + 1)
    FILTERS indexMonth(bm) == indexMonth(b) OR filterBudgetMonths(bm) OR filterBudgetMonths ()
    PROPERTIES filterBudgetMonths() DRAW bm TOOLBAR
    
    EVENTS ON INIT {
        SEEK dashboard.b = budget(currentDate());
    }
;

DESIGN dashboard {
    OBJECTS {
        type = CONTAINERV ;
        NEW headdata {
            type = CONTAINERH;
            MOVE PROPERTY (name(b)) { caption = 'Select Month'; background = RGB (255,255,224);}
            MOVE PROPERTY (planned(b)) { caption = 'Planned, USD'; }
            MOVE PROPERTY (sumUSD) { caption = 'U.S. Amount, USD'; }
            MOVE PROPERTY (sumRUR) { caption = 'Russia Amount, USD'; }
            MOVE PROPERTY (monthExpenses(b)) { caption = 'Total Amount, USD'; }
        }
        NEW deassetails {
            alignment = STRETCH;
            type = SPLITH;
            height = 200;
            MOVE BOX (bm) {
                PROPERTY (monthExpenses(bm)) { caption = 'Total Amount, USD'; }
            }
            MOVE BOX (t) {
                PROPERTY (sumRootUSD) { caption = 'U.S. Amount, USD'; }
                PROPERTY (sumRootRUB) { caption = 'Russia Amount, USD'; }
                PROPERTY (monthCostRootItem(b,t)) { caption = 'Total Amount, USD'; }
            }
        }
        NEW details {
            alignment = STRETCH;
            fill = 1;
            type = SPLITH;
            MOVE BOX (i) {
                PROPERTY (nameCostItem2(i)) { caption = 'Category'; }
                PROPERTY (sumItemUSD) { caption = 'U.S. Amount, USD'; }
                PROPERTY (sumItemRUB) { caption = 'Russia Amount, USD'; }
            }
            MOVE BOX (c) {
                PROPERTY (sumCenterUSD) { caption = 'U.S. Amount, USD'; }
                PROPERTY (sumCenterRUB) { caption = 'Russia Amount, USD'; }
            }
        }
    }
}

NAVIGATOR {
    NEW dashboard BEFORE budgets;
}
