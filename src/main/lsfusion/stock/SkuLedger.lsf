MODULE SkuLedger;

REQUIRE Sku;

NAMESPACE Stock;

CLASS ABSTRACT SkuLedger 'Регистр изменений остатков';
TABLE skuLedger (SkuLedger) FULL;

date 'Дата' = ABSTRACT DATE (SkuLedger);

sku = ABSTRACT Sku (SkuLedger);
nameSku 'Товар' (SkuLedger g) = name(sku(g));
canonicalNameSkuGroup 'Группа товара' (SkuLedger g) = canonicalName(skuGroup(sku(g)));

quantity 'Кол-во' = ABSTRACT NUMERIC[16,5](SkuLedger);
price 'Цена' = ABSTRACT NUMERIC[18,4](SkuLedger);
sum 'Сумма' = ABSTRACT NUMERIC[18,4](SkuLedger);

stock = ABSTRACT Stock(SkuLedger);
nameStock 'Склад' (SkuLedger l) = name(stock(l));

description 'Описание' = ABSTRACT ISTRING[1000](SkuLedger) CHARWIDTH 30;
note 'Примечание' = ABSTRACT ISTRING[250](SkuLedger) CHARWIDTH 15;

FORM skuLedger 'Регистр изменений остатков'
    OBJECTS o = SkuLedger PANEL
    PROPERTIES(o) date, description, note, nameSku, canonicalNameSkuGroup, nameStock, quantity, price, sum
    
    EDIT SkuLedger OBJECT o
;

FORM skuLedgers 'Регистр изменений остатков'
    OBJECTS o = SkuLedger
    PROPERTIES(o) READONLY date, description, note, nameSku, canonicalNameSkuGroup, nameStock, quantity, price, sum
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogSkuLedgers 'Регистр изменений остатков'
    OBJECTS o = SkuLedger
    PROPERTIES(o) READONLY date, description, note, nameSku, canonicalNameSkuGroup, nameStock, quantity, price, sum
    
    LIST SkuLedger OBJECT o
;

currentBalance 'Остаток' (Sku sk, Stock st) = GROUP SUM quantity(SkuLedger l) BY sku(l), stock(l);
currentSum 'Сумма' (Sku sk, Stock st) = GROUP SUM sum(SkuLedger l) BY sku(l), stock(l);
currentPrice 'Цена' (Sku sk, Stock st) = round2(currentSum(sk, st) / currentBalance(sk, st));

filterStock = DATA LOCAL Stock ();
nameFilterStock 'Склад' () = name(filterStock());

FORM stockBalance 'Остаток по складам'
    PROPERTIES nameFilterStock()
    
    TREE treeGroups g = SkuGroup PARENT parent(g)
    PROPERTIES READONLY order(g), id(g) SHOWIF showIDs(), name(g)
    ORDER order(g), name(g)

    OBJECTS ss = (sk = Sku, st = Stock)
    PROPERTIES READONLY 'Товар' = name(sk), 'Склад' = name(st)
    PROPERTIES (sk, st) READONLY currentBalance, currentSum, currentPrice
    ORDER name(sk), name(st)
    FILTERS filterStock() == st OR NOT filterStock()
    FILTERGROUP balance FILTER 'С остатком' currentBalance(sk, st) DEFAULT
    
    OBJECTS d = SkuLedger
    PROPERTIES (d) READONLY date, description, note, quantity, price, sum
    FILTERS sku(d) == sk, stock(d) == st, isParent(skuGroup(sk), g)
    ORDER date(d)
;

DESIGN stockBalance {
    OBJECTS {
        NEW filter {
            caption = 'Фильтры';
            alignment = STRETCH;
            MOVE PROPERTY (nameFilterStock()) { alignment = STRETCH; }
        }
        NEW tables {
            fill = 1;
            type = SPLITH;
            MOVE BOX (TREE treeGroups) { caption = 'Группы товаров'; }
            NEW details {
                fill = 2;
                MOVE BOX (ss);
                MOVE BOX (d);
            }
        }
    }
}

NAVIGATOR {
    stock {
        NEW FOLDER stockDocuments 'Документы';
        NEW FOLDER stockReports 'Отчеты' {
            NEW stockBalance;
        }
    }
}
