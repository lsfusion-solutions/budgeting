MODULE Receipt;

REQUIRE SkuLedger, LegalEntity;

NAMESPACE Stock;

CLASS Receipt 'Приемка';
TABLE receipt (Receipt);

supplier = DATA LegalEntity(Receipt);
nameSupplier 'Поставщик' (Receipt r) = name(supplier(r));

stock = DATA Stock(Receipt);
nameStock 'Склад' (Receipt r) = name(stock(r));

date 'Дата' = DATA DATE (Receipt);
date (Receipt r) <- currentDate() WHEN SET (r IS Receipt);

note 'Примечание' = DATA ISTRING[100] (Receipt) CHARWIDTH 20;

CLASS ReceiptDetail 'Строка приемки' : SkuLedger;
TABLE receiptDetail(ReceiptDetail);

receipt = DATA Receipt(ReceiptDetail) NONULL DELETE INDEXED;

sku = DATA Sku (ReceiptDetail);
nameSku 'Товар' (ReceiptDetail g) = name(sku(g));
canonicalNameSkuGroup 'Группа товара' (ReceiptDetail g) = canonicalName(skuGroup(sku(g)));

quantity 'Кол-во' = DATA NUMERIC[16,5](ReceiptDetail);
price 'Цена' = DATA NUMERIC[18,4](ReceiptDetail);
sum 'Сумма' = DATA NUMERIC[18,4](ReceiptDetail);

WHEN LOCAL (CHANGED (quantity(ReceiptDetail d)) OR CHANGED (price(d))) AND NOT CHANGED (sum(d)) DO {
    sum(d) <- quantity(d) * price(d);
}

sku(ReceiptDetail d) += sku(d);
quantity(ReceiptDetail d) += quantity(d);
price(ReceiptDetail d) += price(d);
sum(ReceiptDetail d) += sum(d);
stock(ReceiptDetail d) += stock(receipt(d));
date(ReceiptDetail d) += date(receipt(d));
description(ReceiptDetail d) += CONCAT ' ', 'Приемка', 'от поставщика ' + nameSupplier(receipt(d));
note(ReceiptDetail d) += note(receipt(d));

quantity 'Кол-во' = GROUP SUM quantity(ReceiptDetail d) BY sku(d), receipt(d);
receiptDetail = GROUP AGGR ReceiptDetail d BY sku(d), receipt(d);

changeQuantityValue (Sku sku, Receipt r, NUMERIC[14,3] n)  { 
    IF receiptDetail(sku, r) THEN {
        quantity(ReceiptDetail d) <- n WHERE d == receiptDetail(sku, r);
    } ELSE NEW d = ReceiptDetail {
        receipt(d) <- r;
        sku(d) <- sku;
        quantity(d) <- n;
    }
    IF NOT n THEN DELETE ReceiptDetail d WHERE d == receiptDetail(sku, r);
}

changeQuantity (Sku sku, Receipt r)  { 
    INPUT n = NUMERIC[16,5] DO
        changeQuantityValue(sku, r, n);
}

FORM receipt 'Приемка'
    OBJECTS o = Receipt PANEL
    PROPERTIES(o) date, nameSupplier, nameStock, note
    
    OBJECTS d = ReceiptDetail
    PROPERTIES (d) nameSku, canonicalNameSkuGroup, quantity, price, sum, NEW, DELETE 
    FILTERS receipt(d) == o
    
    TREE treeGroups g = SkuGroup PARENT parent
    PROPERTIES READONLY order(g), id(g) SHOWIF showIDs(), name(g)
    ORDER order(g), name(g)
    
    OBJECTS sk = Sku
    PROPERTIES (sk) READONLY id SHOWIF showIDs(), name, canonicalNameSkuGroup
    PROPERTIES quantity(sk, o) ON CHANGE changeQuantity(sk, o)
    PROPERTIES (sk) NEWSESSION NEW, EDIT, DELETE
    FILTERS isParent(skuGroup(sk), g)
    
    EDIT Receipt OBJECT o
;

DESIGN receipt {
    OBJECTS {
        NEW tab AFTER BOX (o) {
            type = TABBED;
            fill = 1;
            MOVE BOX (d) { caption = 'Спецификация'; }
            NEW select {
                caption = 'Подбор'; 
                type = SPLITH;
                MOVE BOX (TREE treeGroups) { caption = 'Группы товаров'; }
                MOVE BOX (sk) { fill = 2; }
            }
        }
    }
}

FORM receipts 'Приемка'
    OBJECTS o = Receipt
    PROPERTIES(o) READONLY date, nameSupplier, nameStock, note
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    OBJECTS d = ReceiptDetail
    PROPERTIES (d) READONLY nameSku, canonicalNameSkuGroup, quantity, price, sum
    FILTERS receipt(d) == o
;

FORM dialogReceipts 'Приемка'
    OBJECTS o = Receipt
    PROPERTIES(o) READONLY date, nameSupplier, nameStock, note
    LIST Receipt OBJECT o
;

NAVIGATOR {
    stockDocuments {
        NEW receipts;
    }
}