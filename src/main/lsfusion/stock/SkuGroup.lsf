MODULE SkuGroup;

REQUIRE Stock, Hierarchy;

NAMESPACE Stock;

CLASS SkuGroup 'Классификатор SKU';
TABLE skuGroup(SkuGroup);

name 'Наименование' = DATA ISTRING[250](SkuGroup) MATERIALIZED INDEXED;
order 'Порядок' = DATA INTEGER (SkuGroup) MATERIALIZED CHARWIDTH 2;
id 'Код' = DATA STRING[100] (SkuGroup) IN id CHARWIDTH 10;
skuGroup = GROUP AGGR SkuGroup g BY id(g);

TABLE skuGroupSkuGroup(SkuGroup, SkuGroup);
@defineHierarchy(skuGroup);

@defineHierarchyPlain(skuGroup);
nameGroup1 'Категория' (SkuGroup child) = name(skuGroup1(child));    
nameGroup2 'Направление' (SkuGroup child) = name(skuGroup2(child));    
nameGroup3 'Группа' (SkuGroup child) = name(skuGroup3(child));    
nameGroup4 'Подгруппа' (SkuGroup child) = name(skuGroup4(child));    
nameGroup5 'Субгруппа' (SkuGroup child) = name(skuGroup5(child));    

FORM skuGroup 'Группа товаров'
    OBJECTS g = SkuGroup PANEL
    PROPERTIES(g) name, id SHOWIF showIDs(), nameParent
    EDIT SkuGroup OBJECT g
;

FORM dialogSkuGroups 'Группы товаров'
    OBJECTS g = SkuGroup
    PROPERTIES(g) READONLY name, id SHOWIF showIDs(), nameParent
    LIST SkuGroup OBJECT g
;

FORM skuGroups 'Группы товаров'
    TREE treeGroups g = SkuGroup PARENT parent
    PROPERTIES READONLYIF isReadonly() order(g), id(g) SHOWIF showIDs(), name(g), canonicalName(g)
    PROPERTIES(g) NEWSESSION NEW, EDIT, deleteg=DELETE 
    ORDER order(g), canonicalName(g)
;

NAVIGATOR {
    stockMasterData {
        NEW skuGroups;
    }
}