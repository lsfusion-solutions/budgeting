MODULE PurchaseInvoice;

REQUIRE Currency, Utils;

NAMESPACE Purchase;

CLASS Invoice 'Purchase invoice';
TABLE invoice(Invoice);

date 'Date' = DATA DATE (Invoice);

number 'Number' = DATA STRING[50] (Invoice) CHARWIDTH 15;

nameSupplier 'Supplier' = DATA STRING[50] (Invoice);

description 'Description' (Invoice i) = CONCAT ' / ', number(i), date(i), nameSupplier(i); 

currency 'Currency' = DATA Currency (Invoice);
nameCurrency 'Currency' (Invoice i) = name(currency(i));

amount 'Sum' = DATA NUMERIC[14,2] (Invoice);

note 'Note' = DATA STRING[1000] (Invoice) CHARWIDTH 20;

approved 'Approved' = DATA BOOLEAN (Invoice);
dateTimeApproved 'Date/time of approve' = DATA DATETIME (Invoice);
nameUserApproved 'Approved by' = DATA STRING[200](Invoice) CHARWIDTH 20;

file = DATA FILE (Invoice);
hasFile (Invoice i) = TRUE IF file(i); 
open 'Open Attachment' (Invoice i)  { open(file(i)); }
load 'Load Attachment' (Invoice i)  { INPUT =file(i) CHANGE; }

text '' = DATA RICHTEXT (Invoice);

FORM invoice 'Purchase invoice'
    OBJECTS i = Invoice PANEL
    PROPERTIES(i) date, number, nameSupplier, note, nameCurrency, amount, text
    PROPERTIES(i) open SHOWIF hasFile(i), load
    
    EDIT Invoice OBJECT i
;
DESIGN invoice {
    OBJECTS {
        type = CONTAINERH;
        fill = 1;
        NEW invoice {           
            NEW parameters {
                alignment = STRETCH;
                caption = 'Parameters';
                type = CONTAINERH;
                MOVE PROPERTY(date(i)) { notNull = TRUE; }
                MOVE PROPERTY(number(i)) { notNull = TRUE; }
                MOVE PROPERTY(nameSupplier(i)) { notNull = TRUE; }
            }
            NEW sum {
                alignment = STRETCH;
                caption = 'Sum';
                type = CONTAINERH;
                MOVE PROPERTY(nameCurrency(i)) { notNull = TRUE; }
                MOVE PROPERTY(amount(i)) { notNull = TRUE; }
            }
            NEW file {
                alignment = STRETCH;
                caption = 'File';
                type = CONTAINERH;
                MOVE PROPERTY(open(i)) { fill = 1; height = 40; }
                MOVE PROPERTY(load(i)) { fill = 1; height = 40; }
            }
            NEW note {
                alignment = STRETCH;
                caption = 'Note';
                MOVE PROPERTY(note(i)) { charWidth = 80; }
            }
        }
        NEW pane {
            fill = 2;
            type = TABBED;
            NEW text {
                caption = 'Text';
                fill = 1;
                alignment = STRETCH;
                MOVE PROPERTY (text(i)) { fill = 1; }
            }
        }
    }
}

overCopy ABSTRACT LIST (Invoice, Invoice);

copy 'Copy' (Invoice i) {
    NEWSESSION NEW ni = Invoice {
        date(ni) <- date(i);
        number(ni) <- number(i);
        nameSupplier(ni) <- nameSupplier(i);
        note(ni) <- note(i);
        currency(ni) <- currency(i);
        amount(ni) <- amount(i);
        file(ni) <- file(i);
        text(ni) <- text(i);
        overCopy(ni, i);
        SHOW invoice OBJECTS i = ni DOCKED;
    }
}

FORM dialogInvoices 'Select purchase invoice'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY date, number, nameSupplier, nameCurrency, amount, approved, dateTimeApproved, nameUserApproved
    PROPERTIES(i) open SHOWIF hasFile(i)
    
    LIST Invoice OBJECT i
;

approve 'Approve' (Invoice i) {
    NEWSESSION {
        approved(i) <- TRUE;
        APPLY;
    }
}

//EXTEND FORM invoices 
//    PROPERTIES(i) approve TOOLBAR 
//    
//    FILTERGROUP approved
//        FILTER 'Approved' approved(i) 'F10'
//        FILTER 'Not approved' NOT approved(i) 'F9'       
//;