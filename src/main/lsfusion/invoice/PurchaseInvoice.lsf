MODULE PurchaseInvoice;

REQUIRE CurrencyB, Utils;

NAMESPACE Purchase;

CLASS Invoice 'Purchase invoice';
TABLE invoice(Invoice);

date 'Date' = DATA DATE (Invoice) IN id;

number 'Number' = DATA STRING[50] (Invoice) CHARWIDTH 15 IN id;

nameSupplier 'Supplier' = DATA STRING[50] (Invoice);

description 'Description' (Invoice i) = CONCAT ' / ', number(i), date(i), nameSupplier(i); 

currency 'Currency' = DATA Currency (Invoice);
nameCurrency 'Currency' (Invoice i) = name(currency(i)) IN id;

amount 'Sum' = DATA NUMERIC[14,2] (Invoice) IN id;

note 'Note' = DATA STRING[1000] (Invoice) CHARWIDTH 20;

approved 'Approved' = DATA BOOLEAN (Invoice);
dateTimeApproved 'Date/time of approve' = DATA DATETIME (Invoice);
nameUserApproved 'Approved by' = DATA STRING[200](Invoice) CHARWIDTH 20;

file 'Attachment' = DATA FILE (Invoice);
hasFile (Invoice i) = TRUE IF file(i); 
open 'Open Attachment' (Invoice i)  { open(file(i)); }
load 'Load Attachment' (Invoice i)  { INPUT =file(i) CHANGE; }

CONSTRAINT Invoice i IS Invoice AND (NOT date(i) OR NOT currency(i) OR NOT amount(i) OR NOT file(i)) 
    MESSAGE 'Please provide the required data: Date, Sum, Currency, Budget Item, Attachment';

payment = DATA FILE (Invoice);
hasPayment (Invoice i) = TRUE IF payment(i); 
openPayment 'Open Payment Confirmation' (Invoice i)  { open(payment(i)); }
loadPayment 'Load Payment Confirmation' (Invoice i)  { INPUT =payment(i) CHANGE; }

text '' = DATA RICHTEXT (Invoice);

CLASS ExtraDocumentsLine 'Extra Documents';

invoice = DATA Invoice (ExtraDocumentsLine) NONULL DELETE ;

description 'Description' = DATA STRING[100] (ExtraDocumentsLine);
file 'Document' = DATA FILE (ExtraDocumentsLine);
hasFile (ExtraDocumentsLine i) = TRUE IF file(i); 
open 'Open Document' (ExtraDocumentsLine i)  { open(file(i)); }
load 'Load Document' (ExtraDocumentsLine i)  { INPUT =file(i) CHANGE; }

FORM invoice 'Purchase invoice'
    OBJECTS i = Invoice PANEL
    PROPERTIES(i) date, number, nameSupplier, note, nameCurrency, amount, text
    PROPERTIES(i) open SHOWIF hasFile(i), load,  openPayment SHOWIF hasPayment(i), loadPayment
    
    //EDIT Invoice OBJECT i
;
DESIGN invoice {
    OBJECTS {
        horizontal = TRUE;
        fill = 1;
        NEW invoice {           
            NEW parameters {
                alignment = STRETCH;
                caption = 'Parameters';
                horizontal = TRUE;
                MOVE PROPERTY(date(i)) { notNull = TRUE; }
                MOVE PROPERTY(number(i)) { notNull = TRUE; }
                MOVE PROPERTY(nameSupplier(i)) { notNull = TRUE; }
            }
            NEW sum {
                alignment = STRETCH;
                caption = 'Sum';
                horizontal = TRUE;
                MOVE PROPERTY(nameCurrency(i)) { notNull = TRUE; }
                MOVE PROPERTY(amount(i)) { notNull = TRUE; }
            }
            NEW file {
                alignment = STRETCH;
                caption = 'File';
                horizontal = TRUE;
                MOVE PROPERTY(open(i)) { fill = 1; height = 40; }
                MOVE PROPERTY(load(i)) { fill = 1; height = 40; }
                MOVE PROPERTY(openPayment(i)) { fill = 1; height = 40; }
                MOVE PROPERTY(loadPayment(i)) { fill = 1; height = 40; }
            }
            NEW note {
                alignment = STRETCH;
                caption = 'Note';
                MOVE PROPERTY(note(i)) { charWidth = 80; }
            }
        }
        NEW pane {
            fill = 2;
            tabbed = TRUE;
            NEW text {
                caption = 'Text';
                fill = 1;
                alignment = STRETCH;
                MOVE PROPERTY (text(i)) { fill = 1; }
            }
        }
    }
}

overCopy ABSTRACT LIST (Invoice, Invoice);

copy 'Copy' (Invoice i) {
    NEWSESSION NEW ni = Invoice {
        date(ni) <- date(i);
        number(ni) <- number(i);
        nameSupplier(ni) <- nameSupplier(i);
        note(ni) <- note(i);
        currency(ni) <- currency(i);
        amount(ni) <- amount(i);
        file(ni) <- file(i);
        text(ni) <- text(i);
        overCopy(ni, i);
        SHOW invoice OBJECTS i = ni DOCKED;
    }
}

FORM dialogInvoices 'Select purchase invoice'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY date, number, nameSupplier, nameCurrency, amount, approved, dateTimeApproved, nameUserApproved
    PROPERTIES(i) open SHOWIF hasFile(i)
    
    LIST Invoice OBJECT i
;

approve 'Approve' (Invoice i) {
    NEWSESSION {
        approved(i) <- TRUE;
        APPLY;
    }
}

//EXTEND FORM invoices 
//    PROPERTIES(i) approve TOOLBAR 
//    
//    FILTERGROUP approved
//        FILTER 'Approved' approved(i) 'F10'
//        FILTER 'Not approved' NOT approved(i) 'F9'       
//;